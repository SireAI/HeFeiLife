apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'


//加载项目发布参数配置文件
Properties mainProjectProperty = new Properties()
mainProjectProperty.load(project.rootProject.file('local.properties').newDataInputStream())

//加载库包参数配置文件
Properties subProjectProperty = new Properties()
subProjectProperty.load(project.file('publish.properties').newDataInputStream())
def moduleJcenterName = subProjectProperty.getProperty("module.lib.jcenter.name")
def moduleDesc = subProjectProperty.getProperty("module.desc")
def moduleVersion = subProjectProperty.getProperty("module.veriosn.code")

//发布配置
version = moduleVersion //发布版本号
group = mainProjectProperty.getProperty("project.group.name")
//通过maven插件生成pom文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name mainProjectProperty.getProperty("project.name")
                description mainProjectProperty.getProperty("project.description")
                url mainProjectProperty.getProperty("project.site.url")
                licenses {
                    license {
                        name 'Apache-2.0' //和在建立仓库时选的证件一样
                        url 'https://raw.githubusercontent.com/minggo620/Pluto-Android/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id 'Sire'        //填写bintray或者github的用户名
                        name 'Sire'         //姓名
                        email '1120523212@qq.com'//邮箱
                    }
                }
                scm {
                    connection mainProjectProperty.getProperty("project.site.git.url")
                    developerConnection mainProjectProperty.getProperty("project.site.git.url")
                    url mainProjectProperty.getProperty("project.site.url")
                }
            }
        }
    }
}


bintray {
    user = mainProjectProperty.getProperty("bintray.user")
    key = mainProjectProperty.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = mainProjectProperty.getProperty("bintray.repo.name")//自己创建的仓库名字
        name = moduleJcenterName//上传到JCenter的名字,最终引用的名字
        websiteUrl = mainProjectProperty.getProperty("project.site.url")
        vcsUrl = mainProjectProperty.getProperty("project.site.git.url")
        licenses = ['Apache-2.0']//不能随便写，只能是仓库创建时选择的license type
        userOrg = mainProjectProperty.getProperty("project.organization") //自己创建的organization名称
        publish = true // 是否是公开项目，公开别人可以引用
        version {
            name = moduleJcenterName
            desc = moduleDesc//描述，自己定义
            released  = new Date()
            vcsTag = moduleVersion
        }
    }

}
task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}